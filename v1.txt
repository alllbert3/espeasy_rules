// timer1 - время после отключения света
// timer2 - максимальное время для управления по интернету
// 5,1 arg#time 180, 720 - время работы после отключения света
// 8,1 light#lux - светимость > 621 выкл
// mqtt 100 - выключен, 101 включен
// реле GPIO,14 - 1 выкл, 0 вкл инверсное
// 801-1015 - выключено
// 761-800 - только подсветка
// 571-620 - только свет light#lux
// 520-570 - свет + подсветка
// подсветка < 571-760 < подсветка < 801
// свет < 620

on Lux#Value<[light#lux] do
    if [status#light]=0
        TaskValueSet 7,1,1 // status#light
        timerSet,1,0
        timerSet,2,0
        Publish d1-mini-v/status/light,on
        Publish d1-mini-v/status/vent,101
    endif
    GPIO,14,0 // включить реле
Endon

on Lux#Value>[light#lux] do
    if [status#light]=1

        if [SI7021#Humidity]>60
            Let,1,[arg#time]*4 
            timerSet,1,[VAR#1] 
        else
            timerSet,1,[arg#time]
        endif
        Publish d1-mini-v/status/light,off
        TaskValueSet 7,1,0 // status#light
    endif
Endon

on Rules#Timer=1 do
    if [relay#vent]<>101 //выключить
        GPIO,14,1
        Publish d1-mini-v/status/vent,100
    endif
Endon

on relay#vent do
    if [relay#vent]=101 //включить
        GPIO,14,0
        Publish d1-mini-v/status/vent,101
        Let,2,[arg#time]*10
        timerSet,2,[VAR#2] 
    else 
        GPIO,14,1 //выключить
        Publish d1-mini-v/status/vent,100
        timerSet,2,0
    endif
endon 

on Rules#Timer=2 do
    GPIO,14,1
    timerSet,2,0
    Publish d1-mini-v/status/vent,100
endon
